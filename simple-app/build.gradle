import java.text.SimpleDateFormat

plugins {
    id 'com.gorylenko.gradle-git-properties'
}

dependencies {
    implementation project(':simple-api')

    implementation 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-core'
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mm:ssZ"
    dateFormatTimeZone = "Europe/Zurich"
    extProperty = 'gitProps'
}

generateGitProperties.outputs.upToDateWhen {
    false
}

bootJar {
    archiveFileName = "${project.projectName}-${project.version}.${archiveExtension.get()}"
    manifest {
        attributes(
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision': "${-> project.ext.gitProps['git.commit.id']}",
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

jar {
    enabled = false
}
